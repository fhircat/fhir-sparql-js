# QueryAnalyzer.getArcTrees() test 1
# Reflect order of final ArcTrees.

PREFIX fhir: <http://hl7.org/fhir/>
PREFIX sct: <http://snomed.info/id/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>

SELECT ?obs ?patId WHERE {
  # Observation
  ?obs a fhir:Observation ;					#  0

    # Observation.code
    fhir:code [							#  1
      fhir:coding [						#  2
        rdf:rest*/rdf:first [					#  3
          fhir:code [ fhir:v "72166-2" ] ;			#  4/5
          fhir:system [ fhir:v "http://loinc.org"^^xsd:anyURI ]	#  6/7
        ]
      ]
    ] ;

    # Observation.subject
    fhir:subject [ fhir:reference [ fhir:link ?patRsrc ] ] .	#  8/9/10

  # Patient
  ?patRsrc a fhir:Patient ;					# 10

    # Patient.id
    fhir:id [ fhir:v ?patId ] .					# 11/12
}
